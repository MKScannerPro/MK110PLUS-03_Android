if (!isLibrary.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    compileSdk 34

    defaultConfig {
        if (!isLibrary.toBoolean()) {
            applicationId = "com.moko.mkremotegw03"
        }
        namespace = "com.moko.mkremotegw03"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 3
        versionName "1.0.2"
        def formatDate = new Date().format("yyyyMMddHHmm")
        setProperty("archivesBaseName", "MKRemoteGW03_${versionName}_${formatDate}")
    }

    buildTypes {
        release {
            if (!isLibrary.toBoolean()) {
                buildConfigField("boolean", "IS_LIBRARY", "false")
            } else {
                buildConfigField("boolean", "IS_LIBRARY", "true")
            }
            minifyEnabled false
        }
        debug {
            if (!isLibrary.toBoolean()) {
                buildConfigField("boolean", "IS_LIBRARY", "false")
            } else {
                buildConfigField("boolean", "IS_LIBRARY", "true")
            }
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    viewBinding {
        enabled = true
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            if (!isLibrary.toBoolean()) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            }
        }
    }

    if (isLibrary.toBoolean()) {
        libraryVariants.all { variant ->
            if (variant.buildType.name == "release") {
                // 修改aar名称
                variant.outputs.all { output ->
                    def formatDate = new Date().format("yyyyMMddHHmm")
                    outputFileName = "MKRemoteGW03_${android.defaultConfig.versionName}_${formatDate}.aar"
                }
                // 复制aar到指定目录
                variant.assemble.doLast {
                    variant.outputs.all { output ->
                        // 输出目录在：项目目录/build/outputs/aar/
                        def outputPath = [project.rootDir.absolutePath, "build", "outputs", "aar"].join(File.separator)
                        copy {
                            from output.outputFile
                            into outputPath
                        }
                    }
                }
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/kotlinx_coroutines_core.version'
    }
}

dependencies {
    implementation 'com.lzy.net:okgo:3.0.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.10'
    implementation 'com.github.lzyzsd:circleprogress:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.30'
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation 'com.github.GrenderG:Toasty:1.4.1'
    implementation 'com.guolindev.permissionx:permissionx:1.7.1'
    if (!isLibrary.toBoolean()) {
        implementation project(path: ':mokosupport')
        implementation files('libs/poi-3.12-android-a.jar')
        implementation files('libs/poi-ooxml-schemas-3.12-20150511-a.jar')
    } else {
        implementation files('libs/mokoSupport.jar')
        compileOnly files('libs/mokoBleLib.jar')
        compileOnly files('libs/poi-3.12-android-a.jar')
        compileOnly files('libs/poi-ooxml-schemas-3.12-20150511-a.jar')
        implementation 'com.google.code.gson:gson:2.8.5'
        implementation 'no.nordicsemi.android.support.v18:scanner:1.6.0'
        implementation 'no.nordicsemi.android:dfu:2.3.0'
        implementation 'org.greenrobot:eventbus:3.2.0'
        implementation 'com.elvishew:xlog:1.6.1'
        implementation 'no.nordicsemi.android:ble:2.7.2'
        implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.2'
        //new mqtt library that supports android 12
        implementation 'androidx.legacy:legacy-support-v4:1.0.0'
        implementation 'com.github.hannesa2:paho.mqtt.android:4.2'
        implementation 'org.bouncycastle:bcpkix-jdk15on:1.59'
        implementation 'androidx.core:core:1.3.1'
        implementation 'androidx.appcompat:appcompat:1.0.0'
    }
}
